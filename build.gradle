plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'java'
    id "com.bmuschko.nexus" version "2.3.1"
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

group 'al.aldi'
version '0.1.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.10.0'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.10.0'
    implementation group: 'com.squareup.okio', name: 'okio', version: '1.14.1'
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.2'
    implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.2'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'al.aldi.sprova4j.Main'
        )
    }
}

shadowJar {
    baseName = project.name + '-all'
    classifier = null
    version = null
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'sprova4j'
            version project.version


            from components.java
        }
    }
}

publishing {
    repositories {
        jcenter()
    }
}

nexusStaging {
    stagingProfileId = "sprova4j" //when not defined will be got from server using "packageGroup"
}

modifyPom {
    project {
        name 'sprova4j'
        description 'Java client for Sprova Test Management'
        url 'https://github.com/aldialimucaj/sprova4j'
        inceptionYear '2018'
        scm {
            url 'https://github.com/aldialimucaj/sprova4j'
            connection 'scm:https://github.com/aldialimucaj/sprova4j.git'
            developerConnection 'scm:ghttps://github.com/aldialimucaj/sprova4j.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'aldi'
                name 'Aldi Alimucaj'
                email 'aldi.alimucaj@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}